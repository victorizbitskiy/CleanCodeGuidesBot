#### Не перехватывайте неожиданные исключения, а перенаправляйте их

> [Чистый ABAP] > [Содержание] > [Тестирование] > [Утверждения] > [Эта секция]

```ABAP
METHODS reads_entry FOR TESTING RAISING /clean/some_exception.

METHOD reads_entry.
  "when
  DATA(entry) = cut->read_something( ).
  "then
  cl_abap_unit_assert=>assert_not_initial( entry ).
ENDMETHOD.
```

Ваш тестовый код по-прежнему ориентирован на благополучный исход, поэтому его намного легче читать и понимать по сравнению с:

```ABAP
" anti-pattern
METHOD reads_entry.
  TRY.
      DATA(entry) = cut->read_something( ).
    CATCH /clean/some_exception INTO DATA(unexpected_exception).
      cl_abap_unit_assert=>fail( unexpected_exception->get_text( ) ).
  ENDTRY.
  cl_abap_unit_assert=>assert_not_initial( entry ).
ENDMETHOD.
```
